
apply plugin: 'java-library'

/*用于上传maven*/
apply plugin: 'maven-publish'

java {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def NEXUS_USER = properties.getProperty('NEXUS_USER')
def NEXUS_PWD = properties.getProperty('NEXUS_PWD')
def NEXUS_URL = properties.getProperty('NEXUS_URL')
def NEXUS_URL_SNAPSHOT  = properties.getProperty('NEXUS_URL_SNAPSHOT')

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.java
                groupId = GROUP_ID
                artifactId = ARTIFACT_ID
                version = PROJECT_VERSION
            }
        }

        repositories {
            //仓库配置
            maven {
                //对于非 https 的仓库地址，需要设置 allowInsecureProtocol true
                allowInsecureProtocol true
                name = PROJECT_NAME //可选
                url = PROJECT_VERSION.endsWith('SNAPSHOT') ? NEXUS_URL_SNAPSHOT : NEXUS_URL
                credentials {
                    username = NEXUS_USER
                    password = NEXUS_PWD
                }
            }
        }
    }
}
